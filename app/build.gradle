plugins {
    id('com.android.application') version '8.3.0'
}

tasks.withType(AbstractArchiveTask).configureEach {  // Make build reproducible https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

repositories {
    google()
    mavenCentral()
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId 'org.gradle.samples'
        namespace 'org.gradle.samples'
        minSdkVersion 16
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 30
        versionCode 1
        versionName '1.0'
        //testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    signingConfigs {
        release {
            storeFile file('../keystore.jks')
            storePassword 'secret-store-password'
            keyAlias 'release-key'
            keyPassword 'secret-key-password'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        releaseAgainButUnsigned {
            initWith release
            signingConfig null
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    //testImplementation 'junit:junit:4.13.1'
    //androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
