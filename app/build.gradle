plugins {
    id('com.android.application') version '8.3.0'
}

// Make build reproducible, refer to https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

repositories {
    google()
    mavenCentral()
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId 'org.gradle.samples'
        namespace 'org.gradle.samples'
        minSdkVersion 16
        // Refer to https://docs.clover.com/dev/docs/setting-android-sdk-versions#troubleshoot-issues-with-target-sdk-level
        // noinspection ExpiredTargetSdkVersion
        targetSdkVersion 30
        versionCode 1
        versionName '1.0'
    }
    signingConfigs {
        release {
            storeFile file('../keystore.jks')
            storePassword 'secret-store-password'
            keyAlias 'release-key'
            keyPassword 'secret-key-password'
        }
    }
    buildTypes {
        release {
            debuggable false
            shrinkResources true
            minifyEnabled true
            signingConfig signingConfigs.release
        }
        releaseAgainButUnsigned {
            initWith release
            signingConfig null
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
}
